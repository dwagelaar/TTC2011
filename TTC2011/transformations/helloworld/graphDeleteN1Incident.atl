-- @atlcompiler emftvm
-- @path Graph=/TTC2011/metamodels/graph.ecore
module graphDeleteN1Incident;

create OUT : Graph from IN : Graph;

--
-- Helpers
--

helper context Graph!Edge def : targetsN1 : Boolean =
	self.trg.isN1;

helper context OclAny def : isN1 : Boolean =
	false;

helper context Graph!Node def : isN1 : Boolean =
	self.name = 'n1';

helper context OclAny def : notN1 : Graph!Node =
	if self.isN1 then
		OclUndefined
	else
		self
	endif;

--
-- Rules
--

rule Graph {
	from s : Graph!Graph
	to t : Graph!Graph (
		nodes <- s.nodes->reject(n | n.isN1),
		edges <- s.edges->reject(e | e.targetsN1))
}

rule Node {
	from s : Graph!Node (not s.isN1)
	to t : Graph!Node (
		name <- s.name)
}

rule Edge {
	from s : Graph!Edge (not s.targetsN1)
	to t : Graph!Edge (
		src <- s.src.notN1,
		trg <- s.trg.notN1)
}